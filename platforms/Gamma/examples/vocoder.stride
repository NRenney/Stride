use Gamma version 1.0 on PC
import generators
import filters

signal CarrierFrequency { value: 196.0 rate: none reset: Reset }
constant CenterFrequencies [8] { value: [ 82.5, 165.0, 330.0, 660.0, 1320.0, 2640.0, 5280.0, 10560.0 ] }
constant  QFactor { value: 6.0 }
trigger Reset {}

AudioIn[1]
>> BandPass ( frequency: CenterFrequencies qFactor: QFactor gain: 5.0 )
>> Rectify ( mode: 'Full' )
>> LowPass ( frequency: 50.0 qFactor: 0.707 gain: 3.0 )
>> AmplitudeEnvelope;

BandLimitedSaw ( frequency: CarrierFrequency )
>> BandPass ( frequency: CenterFrequencies qFactor: QFactor gain: AmplitudeEnvelope )
>> Mix ()
>> AudioOut[1];

reaction UpdateCarrierFrequency (
    terminateWhen:  Updated
    blocks:         trigger Updated {}
    streams:        [
        CarrierFrequency * 1.059463
        >> CarrierFrequency
        >> Compare ( value: 1568.0 operator: 'Greater' )
        >> Reset;

        on >> Updated;
    ]
)

DigitalIn[1] >> UpdateCarrierFrequency();
